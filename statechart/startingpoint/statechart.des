STATECHART:
    top [DS]
        Watch [DS]
            Backlight [CS] [DS] [HS]
                BacklightOff [DS]
                WaitingPeriod
                BacklightOn
            TimeTicking [CS] [DS]
                TimeRunning [DS]
                TimeStopped
            Modes [CS] [DS]
                TimeDisplayMode [DS]
                ChronoMode [HS]
                    ChronoDisplay [DS]
                    ChronoRefresh
                TimeEditingMode
                    LoopSelection
                    ExitWait
                    IncreaseSelection
                    StartEditing [DS]
                TimeEditingModeWait
                ReturnFromEditMode
                DisplayAlarm
                ToggleAlarm
            ChronoTicking [CS] [DS]
                ChronoStopped [DS]
                ChronoRunning
            EditingModeExitTimer [CS] [DS]
                TimerStop [DS]
                TimerRunning
                Reset
            AlarmMode [CS] [DS]
                AlarmNotSet [DS]
                CheckAlarm
                RiseAlarm
                    BacklightOn [DS]
                    BacklightOff
        DisplayEights
        ClearDisplay

ENTER:
   N: top.DisplayEights
   O: gui.drawLowBattery()

ENTER:
   N: top.Watch.Backlight.BacklightOn
   O: gui.setIndiglo()

ENTER:
   N: top.Watch.ChronoTicking.ChronoRunning
   O: gui.increaseChronoByOne()

ENTER:
   N: top.Watch.Backlight.BacklightOff
   O: gui.unsetIndiglo()

ENTER:
   N: top.Watch.Modes.TimeEditingModeWait
   O: gui.refreshTimeDisplay()

ENTER:
   N: top.Watch.AlarmMode.RiseAlarm.BacklightOn
   O: gui.setIndiglo()

EXIT:
   S: top.Watch.Modes.TimeEditingMode
   O: gui.stopSelection()

ENTER:
   N: top.Watch.Modes.ToggleAlarm
   O: gui.setAlarm();[EVENT("toggleAlarm")]

ENTER:
   N: top.Watch.Modes.TimeEditingMode.StartEditing
   O: gui.startSelection()

ENTER:
   N: top.Watch.Modes.TimeEditingMode.ExitWait
   O: [EVENT("resetEvent")]

ENTER:
   N: top.Watch.TimeTicking.TimeRunning
   O: gui.increaseTimeByOne()

ENTER:
   N: top.Watch.AlarmMode.CheckAlarm
   O: gui.checkTime()

ENTER:
   N: top.Watch.Modes.ChronoMode.ChronoDisplay
   O: gui.refreshChronoDisplay()

ENTER:
   N: top.Watch.AlarmMode.RiseAlarm.BacklightOff
   O: gui.unsetIndiglo()

ENTER:
   N: top.Watch.Modes.ChronoMode.ChronoRefresh
   O: gui.refreshChronoDisplay()

ENTER:
   N: top.ClearDisplay
   O: gui.clearDisplay()

ENTER:
   N: top.Watch.Modes.DisplayAlarm
   O: gui.refreshAlarmDisplay()

ENTER:
   N: top.Watch.Modes.TimeDisplayMode
   O: gui.refreshTimeDisplay()

ENTER:
   N: top.Watch.Modes.TimeEditingMode.IncreaseSelection
   O: [EVENT("resetEvent")];gui.increaseSelection()

ENTER:
   N: top.Watch.Modes.ReturnFromEditMode
   O: gui.refreshTimeDisplay()

ENTER:
   N: top.Watch.EditingModeExitTimer.Reset
   O: [EVENT("resetTimer")]

TRANSITION:
    S: top.Watch.Backlight.BacklightOff
    N: top.Watch.Backlight.BacklightOn
    E: topRightPressed

TRANSITION:
    S: top.Watch.Backlight.BacklightOn
    N: top.Watch.Backlight.WaitingPeriod
    E: topRightReleased

TRANSITION:
    S: top.Watch.Backlight.WaitingPeriod
    N: top.Watch.Backlight.BacklightOff
    T: 2

TRANSITION:
    S: top.Watch.Backlight.WaitingPeriod
    N: top.Watch.Backlight.BacklightOn
    E: topRightPressed

TRANSITION:
    S: top.Watch.Modes.ChronoMode
    N: top.Watch.Modes.TimeDisplayMode
    E: topLeftPressed

TRANSITION:
    S: top.Watch.TimeTicking.TimeRunning
    N: top.Watch.TimeTicking.TimeRunning
    T: 1

TRANSITION:
    S: top.Watch.ChronoTicking.ChronoStopped
    N: top.Watch.ChronoTicking.ChronoRunning
    E: startChrono

TRANSITION:
    S: top.Watch.ChronoTicking.ChronoRunning
    N: top.Watch.ChronoTicking.ChronoRunning
    T: 0.01

TRANSITION:
    S: top.Watch.ChronoTicking.ChronoRunning
    N: top.Watch.ChronoTicking.ChronoStopped
    E: stopChrono

TRANSITION:
    S: top.Watch.Modes.ChronoMode.ChronoDisplay
    N: top.Watch.Modes.ChronoMode.ChronoRefresh
    E: bottomRightPressed
    O: [EVENT("startChrono")]

TRANSITION:
    S: top.Watch.Modes.ChronoMode.ChronoRefresh
    N: top.Watch.Modes.ChronoMode.ChronoRefresh
    T: 0.01

TRANSITION:
    S: top.Watch.Modes.ChronoMode.ChronoRefresh
    N: top.Watch.Modes.ChronoMode.ChronoDisplay
    E: bottomRightPressed
    O: [EVENT("stopChrono")]

TRANSITION:
    S: top.Watch.Modes.ChronoMode.ChronoDisplay
    N: top.Watch.Modes.ChronoMode.ChronoDisplay
    E: bottomLeftPressed
    O: gui.resetChrono()

TRANSITION:
    S: top.Watch.Modes.ChronoMode.ChronoRefresh
    N: top.Watch.Modes.ChronoMode.ChronoDisplay
    E: bottomLeftPressed
    O: [EVENT("stopChrono")];gui.resetChrono()

TRANSITION: [HS]
    S: top.Watch.Modes.TimeDisplayMode
    N: top.Watch.Modes.ChronoMode
    E: topLeftPressed

TRANSITION:
    S: top.Watch.TimeTicking.TimeRunning
    N: top.Watch.TimeTicking.TimeStopped
    E: stopTime

TRANSITION:
    S: top.Watch.TimeTicking.TimeStopped
    N: top.Watch.TimeTicking.TimeRunning
    E: startTime

TRANSITION:
    S: top.Watch.Modes.TimeEditingMode.LoopSelection
    N: top.Watch.Modes.TimeEditingMode.ExitWait
    E: bottomRightPressed

TRANSITION:
    S: top.Watch.Modes.TimeEditingMode.ExitWait
    N: top.Watch.Modes.TimeEditingMode.LoopSelection
    E: bottomRightReleased
    O: gui.selectNext()

TRANSITION:
    S: top.Watch.Modes.TimeEditingMode.LoopSelection
    N: top.Watch.Modes.TimeEditingMode.IncreaseSelection
    E: bottomLeftPressed

TRANSITION:
    S: top.Watch.Modes.TimeEditingMode.IncreaseSelection
    N: top.Watch.Modes.TimeEditingMode.LoopSelection
    E: bottomLeftReleased

TRANSITION:
    S: top.Watch.Modes.TimeEditingMode.IncreaseSelection
    N: top.Watch.Modes.TimeEditingMode.IncreaseSelection
    T: 0.3

TRANSITION:
    S: top.Watch.Modes.TimeEditingMode.StartEditing
    N: top.Watch.Modes.TimeEditingMode.LoopSelection
    E: bottomRightReleased
    O: [EVENT("startTimer")]

TRANSITION:
    S: top.Watch.Modes.TimeDisplayMode
    N: top.Watch.Modes.TimeEditingModeWait
    E: bottomRightPressed

TRANSITION:
    S: top.Watch.Modes.TimeEditingModeWait
    N: top.Watch.Modes.TimeDisplayMode
    E: bottomRightReleased

TRANSITION:
    S: top.Watch.Modes.TimeEditingModeWait
    N: top.Watch.Modes.TimeEditingMode
    T: 1.5
    O: [EVENT("stopTime")]

TRANSITION:
    S: top.Watch.Modes.TimeDisplayMode
    N: top.Watch.Modes.TimeDisplayMode
    T: 1

TRANSITION:
    S: top.Watch.Modes.ReturnFromEditMode
    N: top.Watch.Modes.TimeDisplayMode
    E: bottomRightReleased

TRANSITION:
    S: top.Watch.Modes.ReturnFromEditMode
    N: top.Watch.Modes.ReturnFromEditMode
    T: 1

TRANSITION:
    S: top.Watch.Modes.TimeEditingMode.ExitWait
    N: top.Watch.Modes.ReturnFromEditMode
    T: 2
    O: [EVENT("startTime")]

TRANSITION:
    S: top.Watch.EditingModeExitTimer.TimerStop
    N: top.Watch.EditingModeExitTimer.TimerRunning
    E: startTimer

TRANSITION:
    S: top.Watch.EditingModeExitTimer.TimerRunning
    N: top.Watch.EditingModeExitTimer.TimerStop
    E: stopTimer

TRANSITION:
    S: top.Watch.EditingModeExitTimer.TimerRunning
    N: top.Watch.EditingModeExitTimer.TimerStop
    T: 5
    O: [EVENT("exitEditingMode")]

TRANSITION:
    S: top.Watch.Modes.TimeEditingMode
    N: top.Watch.Modes.TimeDisplayMode
    E: exitEditingMode
    O: [EVENT("startTime")]

TRANSITION:
    S: top.Watch.EditingModeExitTimer.TimerRunning
    N: top.Watch.EditingModeExitTimer.Reset
    E: resetEvent

TRANSITION:
    S: top.Watch.EditingModeExitTimer.Reset
    N: top.Watch.EditingModeExitTimer.TimerRunning
    E: resetTimer

TRANSITION:
    S: top.Watch.Modes.TimeDisplayMode
    N: top.Watch.Modes.DisplayAlarm
    E: bottomLeftPressed

TRANSITION:
    S: top.Watch.Modes.TimeEditingMode.StartEditing
    N: top.Watch.Modes.TimeEditingMode.LoopSelection
    E: bottomLeftReleased
    O: [EVENT("startTimer")]

TRANSITION:
    S: top.Watch.Modes.DisplayAlarm
    N: top.Watch.Modes.ToggleAlarm
    E: bottomLeftReleased

TRANSITION:
    S: top.Watch.Modes.ToggleAlarm
    N: top.Watch.Modes.TimeDisplayMode
    E: bottomLeftPressed

TRANSITION:
    S: top.Watch.AlarmMode.AlarmNotSet
    N: top.Watch.AlarmMode.CheckAlarm
    E: toggleAlarm

TRANSITION:
    S: top.Watch.AlarmMode.CheckAlarm
    N: top.Watch.AlarmMode.AlarmNotSet
    E: toggleAlarm

TRANSITION:
    S: top.Watch.AlarmMode.CheckAlarm
    N: top.Watch.AlarmMode.CheckAlarm
    T: 1

TRANSITION:
    S: top.Watch.AlarmMode.CheckAlarm
    N: top.Watch.AlarmMode.RiseAlarm
    E: alarmStart

TRANSITION:
    S: top.Watch.AlarmMode.RiseAlarm.BacklightOn
    N: top.Watch.AlarmMode.RiseAlarm.BacklightOff
    T: 0.5

TRANSITION:
    S: top.Watch.AlarmMode.RiseAlarm.BacklightOff
    N: top.Watch.AlarmMode.RiseAlarm.BacklightOn
    T: 0.5

TRANSITION:
    S: top.Watch.AlarmMode.RiseAlarm
    N: top.Watch.AlarmMode.CheckAlarm
    T: 4

TRANSITION: [HS]
    S: top.DisplayEights
    N: top.Watch.Backlight
    E: rechargeBatteryRequested
    O: gui.refreshDateDisplay()

TRANSITION:
    S: top.Watch
    N: top.DisplayEights
    T: 75

TRANSITION:
    S: top.DisplayEights
    N: top.ClearDisplay
    T: 5

TRANSITION:
    S: top.Watch.Modes.DisplayAlarm
    N: top.Watch.Modes.TimeEditingMode
    T: 1.5
